apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 33
    buildToolsVersion = "33.0.0"
    if (findProperty("androidNdkVersion")) {
        ndkVersion androidNdkVersion // Correct NDK setup is necessary for stripping native symbols in release build
    }
    testBuildType System.getProperty('testBuildType', 'debug').toLowerCase()
    defaultConfig {
        applicationId "com.twilio.conversations.app"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "ACCESS_TOKEN_SERVICE_URL", "\"$ACCESS_TOKEN_SERVICE_URL\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-test-rules.pro'

            // set debuggable flag in order to make it possible for mockito to create mocks in release build.
            // release apk must be repacked without this flag before publishing
            debuggable true

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true

                println "release: unstrippedNativeLibsDir \"build/intermediates/merged_native_libs/release/out/lib\""
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib"
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true

                println "debug: unstrippedNativeLibsDir \"build/intermediates/merged_native_libs/debug/out/lib\""
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/debug/out/lib"
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/sharedTest"
        }
        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs += [
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi",
                "-Xopt-in=kotlinx.coroutines.FlowPreview"]
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen {false}
                showStandardStreams = true
            }
        }
    }

    packagingOptions {
        exclude '**/attach_hotspot_windows.dll'
        exclude 'META-INF/licenses/**'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {
    // Twilio
    implementation "com.twilio:conversations-android-with-symbols:5.1.0"
    // or without symbols:
    // implementation "com.twilio:conversations-android:5.0.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-datetime:0.2.1"

    // Android
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation 'androidx.emoji:emoji-bundled:1.1.0'

    // Room components
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Material
    implementation 'com.google.android.material:material:1.4.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-core:19.0.2'
    implementation 'com.google.firebase:firebase-iid:21.1.0'
    implementation 'com.google.firebase:firebase-messaging:22.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.3'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:18.2.3'
    implementation 'com.google.firebase:firebase-analytics:19.0.2'

    // Support for the new language APIs
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines_version"
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'org.mockito:mockito-android:4.0.0'
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines_version"
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.json:json:20200518'
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"
}

if (hasProperty("googleServicesJson")) {
    copy {
        from(googleServicesJson)
        into(projectDir)
    }
}

// Fix Powermocks on JDK-17: https://stackoverflow.com/questions/69896191/powermock-compatibility-with-jdk-17
tasks.withType(Test).configureEach {
    jvmArgs += [
            '--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED',
            '--add-opens=java.base/java.util=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/java.lang.invoke=ALL-UNNAMED',
            '--add-opens=java.prefs/java.util.prefs=ALL-UNNAMED',
            '--add-opens=java.base/java.nio.charset=ALL-UNNAMED',
            '--add-opens=java.base/java.net=ALL-UNNAMED'
    ]
}
