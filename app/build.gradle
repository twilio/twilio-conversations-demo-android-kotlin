apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    testBuildType System.getProperty('testBuildType', 'debug').toLowerCase()
    defaultConfig {
        applicationId "com.twilio.conversations.app"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "ACCESS_TOKEN_SERVICE_URL", "\"$ACCESS_TOKEN_SERVICE_URL\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-test-rules.pro'

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true

                println "release: unstrippedNativeLibsDir \"build/intermediates/merged_native_libs/release/out/lib\""
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/release/out/lib"

                println "release: strippedNativeLibsDir \"build/intermediates/stripped_native_libs/release/out/lib\""
                strippedNativeLibsDir "build/intermediates/stripped_native_libs/release/out/lib"
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true

                println "debug: unstrippedNativeLibsDir \"build/intermediates/merged_native_libs/debug/out/lib\""
                unstrippedNativeLibsDir "build/intermediates/merged_native_libs/debug/out/lib"

                println "debug: strippedNativeLibsDir \"build/intermediates/stripped_native_libs/debug/out/lib\""
                strippedNativeLibsDir "build/intermediates/stripped_native_libs/debug/out/lib"
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/sharedTest"
        }
        androidTest {
            java.srcDirs += "$projectDir/src/sharedTest"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.FlowPreview"]
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }

    packagingOptions {
        exclude '**/attach_hotspot_windows.dll'
        exclude 'META-INF/licenses/**'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    buildFeatures {
        dataBinding = true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

dependencies {
    // Twilio
    implementation "com.twilio:conversations-android-with-symbols:1.4.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"

    // Android
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation 'androidx.emoji:emoji-bundled:1.1.0'

    // Room components
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Material
    implementation 'com.google.android.material:material:1.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Firebase
    implementation 'com.google.firebase:firebase-core:18.0.2'
    implementation 'com.google.firebase:firebase-iid:21.0.1'
    implementation 'com.google.firebase:firebase-messaging:21.0.1'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.0'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.4.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.3'

    // Test
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines_version"
    androidTestImplementation 'com.google.truth:truth:1.0.1'
    androidTestImplementation 'org.mockito:mockito-android:3.2.4'
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_coroutines_version"
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.json:json:20200518'
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"
}

if (hasProperty("googleServicesJson")) {
    copy {
        from(googleServicesJson)
        into(projectDir)
    }
}
